group 'org.fero'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'gradle-one-jar'

buildscript {
    repositories {
        mavenCentral();
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}

repositories {
    mavenCentral();
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'io.dropwizard:dropwizard-core:0.9.2'
}

clean.doFirst {
    delete 'logs'
}

/**
 * Set up main class and define how to run the server.
 */
mainClassName = "org.fero.siliconlove.SiliconLoveApplication"

project.ext {
    configPath = "$rootProject.projectDir/conf/SiliconLove.yml"
    executionDir = 'opt/'
}

run {
    args 'server', configPath
}

/**
 * Build a fat jar.
 */
task fatJar(type: OneJar) {
    mainClass = mainClassName
    archiveName = 'silicon-love-fat.jar'
}

/**
 * Define docker related tasks.
 */

docker {
    baseImage "ubuntu:latest"
    maintainer 'Wei Guo <wei.mybox@gmail.com>'
}

task buildDocker(type: Docker, dependsOn: fatJar) {
    applicationName = 'silicon-love'

    /*
     * Update system
     */
    runCommand 'apt-get update'
    runCommand 'apt-get install -y software-properties-common'

    /*
     * Install java 8
     */
    runCommand 'add-apt-repository -y ppa:webupd8team/java'
    runCommand 'apt-get update'
    runCommand 'echo oracle-java8-installer shared/accepted-oracle-license-v1-1 select true | sudo /usr/bin/debconf-set-selections'
    runCommand 'apt-get install -y oracle-java8-installer'

    /*
     * Install mysql
     */
    runCommand 'apt-get install -y mysql-client mysql-server'
    // Overwrite mysql cnf
    addFile 'conf/my.cnf', '/etc/mysql/my.cnf'

    /*
     * Add all required files into docker
     */
    addFile 'build/libs/silicon-love-fat.jar', executionDir
    addFile configPath, executionDir

    /*
     * Expose server and mysql port
     */
    exposePort 8080
    exposePort 3306
}