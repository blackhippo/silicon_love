group 'org.fero'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'java'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

project.ext {
    configPath = "$rootProject.projectDir/conf/SiliconLove.yml"
    executionDir = 'opt/'
    dbName = 'silicon-love-db'
    dropwizardVersion = '0.9.2'
}

repositories {
    mavenCentral();
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile (
        'mysql:mysql-connector-java:5.1.38',
        'io.dropwizard:dropwizard-core:' + dropwizardVersion,
        'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
        'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion
    )
}

clean.doFirst {
    delete 'logs'
}

/**
 * Set up main class and define how to run the server.
 */
mainClassName = "org.fero.siliconlove.SiliconLoveApplication"

jar {
    manifest {
        attributes (
            'Main-Class': mainClassName,
            'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

run {
    args 'server', configPath
}

/**
 * Copy dependencies into libs
 */
task copyDependencies(type: Copy) {
    into 'build/libs'
    from configurations.runtime
}

/**
 * Define docker related tasks.
 */

docker {
    maintainer 'Wei Guo <wei.mybox@gmail.com>'
}

/*
 * MySql related tasks
 */
def checkMySql() {
    def output = new ByteArrayOutputStream()
    exec {
        commandLine 'docker', 'ps', '-a'
        standardOutput = output
    }
    return output.toString()
}

task stopMySql(type: Exec) {
    // Stop the db container if there was one.
    if (checkMySql().contains(dbName)) {
        commandLine 'docker', 'stop', dbName
        commandLine 'docker', 'rm', dbName
    } else {
        commandLine 'echo', '"No DB started"'
    }
}

// Fresh start mysql db container.
task startMySql(type: Exec, dependsOn: stopMySql) {
    commandLine 'docker', 'run', '--name', dbName,
            '-e', 'MYSQL_ROOT_PASSWORD=test',
            '-e', 'MYSQL_DATABASE=silicon-love',
            '-d', 'mysql:latest'
}

task buildApp(type: Docker, dependsOn: copyDependencies) {
    applicationName = 'silicon-love-app'
    tagVersion = '1.0'

    baseImage 'java:8'

    /*
     * Add all required files into docker
     */
    addFile 'build/libs', executionDir + '/libs'
    addFile configPath, executionDir

    /*
     * Expose server and mysql port
     */
    exposePort 8080
}